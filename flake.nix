{
  description = "Shade - Skill's Hyprland Adwaita Desktop Environment";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
    astal = {
      url = "github:aylur/astal";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    hyprland = {
      url = "github:hyprwm/Hyprland";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      astal,
      ...
    }@inputs:
    let
      system = "x86_64-linux";
      pkgs = nixpkgs.legacyPackages.${system};

      astalPackages = with astal.packages.${system}; [
        apps
        auth
        battery
        bluetooth
        brightness
        cava
        greet
        astal.packages.${system}.hyprland
        mpris
        network
        notifd
        powerprofiles
        quarrel
        tray
        wireplumber
        astal4
        wl
      ];

      nativeBuildInputs = with pkgs; [
        wrapGAppsHook4
        gobject-introspection
        esbuild # gnim-schemas cli bundles schema.ts with it
        libxml2 # xmllint, validates the generated gschema
        nodejs
      ];

      buildInputs =
        with pkgs;
        [
          gsettings-desktop-schemas
          glib
          libadwaita
          libgtop
          libgweather
          libglycin-gtk4
          glycin-loaders
          glib-networking
          gtk4
          gtk4-layer-shell
          gjs
          libsecret
        ]
        ++ astalPackages;

      wrapperPackages = with pkgs; [
        hyprland
        greetd
        bash
        curl
        grim
        imagemagick
        wl-screenrec
        wf-recorder
        wayfreeze
        wl-clipboard
        hyprsunset
        hypridle
        matugen
        glib.bin
        uwsm
        pipewire
      ];
    in
    {
      packages.${system} = {
        default = import ./nix/desktop-shell.nix {
          inherit
            pkgs
            buildInputs
            nativeBuildInputs
            wrapperPackages
            ;
          inherit (pkgs) pnpmConfigHook fetchPnpmDeps;
          lib = pkgs.lib;
        };
      };

      nixosModules.default = import ./nix/module.nix inputs;
      homeManagerModules.default = import ./nix/home inputs;

      devShells.${system} = import ./nix/devshell.nix {
        inherit
          pkgs
          buildInputs
          nativeBuildInputs
          wrapperPackages
          ;
      };
      nixosConfigurations.vm = nixpkgs.lib.nixosSystem {
        inherit system;
        modules = [
          self.nixosModules.default
        ];
      };
    };
}
